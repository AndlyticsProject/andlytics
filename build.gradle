buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    maven { 
        url "https://repository-achartengine.forge.cloudbees.com/snapshot/"
    }
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}

dependencies {
    compile "com.android.support:design:22.2.1"
    compile "org.achartengine:achartengine:1.1.0"
    compile "ch.acra:acra:4.6.1"
    compile 'com.google.android.gms:play-services:4.0.30'
    compile 'org.apache.commons:commons-lang3:3.1'

    compile ('com.google.api-client:google-api-client:1.19.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'xpp3'
        exclude group: 'junit'
        exclude group: 'com.google.android'
    }
    compile 'com.google.api-client:google-api-client-android:1.19.0'
    compile 'com.google.api-client:google-api-client-gson:1.19.0'
    compile 'com.google.apis:google-api-services-adsense:v1.4-rev29-1.19.0'
    compile 'com.google.code.gson:gson:2.1'
    compile 'net.sf.opencsv:opencsv:2.3'
    compile 'com.google.code.findbugs:jsr305:1.3.9'
}

android {
    flavorDimensions "default"

	compileSdkVersion 22
	buildToolsVersion '26.0.2'

    // lintOptions {
    //     checkReleaseBuilds false
    //     abortOnError false
    // }

    defaultConfig {
        versionCode 269
        versionName '2.8.3'
    }

    signingConfigs {
        if (rootProject.file('andlytics-release-key.keystore').exists()) {
            release {
                storeFile file ("andlytics-release-key.keystore")
            }
        }
        gh {
            storeFile file ('keystore/andlytics.jks')
            keyAlias    'andlytics'
            keyPassword 'andlytics'
            storePassword 'andlytics'
        }
    }

    buildTypes {
        release {
            if (rootProject.file('andlytics-release-key.keystore').exists()) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            if (rootProject.file('andlytics-release-key.keystore').exists()) {
                signingConfig signingConfigs.release
            }
        }
        releaseUnsigned.initWith(buildTypes.release)
        releaseUnsigned {
            signingConfig null
        }

        gh {
            signingConfig signingConfigs.gh
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    productFlavors {
        playstore {
            versionNameSuffix "-playstore"
        }
        nobackup {
            versionNameSuffix "-nobackup"
        }
    }

	sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        playstore { 
            manifest.srcFile 'AndroidManifest-backup.xml'
        }

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
}

// get signing config from gradle.properties file if it exists
// See Andlytics Dropbox folder for example
if (project.hasProperty('storePassword')
        && project.hasProperty('keyAlias')
        && project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyAlias = keyAlias
    android.signingConfigs.release.keyPassword = keyPassword
}

android.applicationVariants.all { v ->
   /* v.outputs.each { out ->
        def fileName = out.outputFile.toString()
        def idx = fileName.lastIndexOf('/') + 1
        fileName = fileName.substring(0, idx)
        fileName += "andlytics-" + v.buildType.name + '-' + android.defaultConfig.versionName + '.apk'
        out.outputFile = new File(fileName)

    }*/
    v.outputs.all { output ->
        //def fileName = out.outputFile.toString()
        def fileName = outputFileName
        def idx = fileName.lastIndexOf('/') + 1
        fileName = fileName.substring(0, idx)
        fileName += "andlytics-" + v.buildType.name + '-' + android.defaultConfig.versionName + '.apk'
//        out.outputFile = new File(fileName)
        outputFileName = new File(fileName)
    }
}

